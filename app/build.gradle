apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'

def timestamp = new Date().getTime()

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'
    def versionPropsFile = file('version.properties')
    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()

        versionProps.load(new FileInputStream(versionPropsFile))

        def value = 0

        def runTasks = gradle.startParameter.taskNames
        if ('assemble' in runTasks || 'assembleRelease' in runTasks || 'aR' in runTasks) {
            value = 1
        }
        if ('packageDebug' in runTasks) {
            value = 2
        }

        def versionMajor = 0
        def versionMinor = 4
        def versionPatch = versionProps['VERSION_PATCH'].toInteger() + value
        def versionBuild = versionProps['VERSION_BUILD'].toInteger() + 1

        versionProps['VERSION_PATCH'] = versionPatch.toString()
        versionProps['VERSION_BUILD'] = versionBuild.toString()

        versionProps.store(versionPropsFile.newWriter(), null)

        defaultConfig {
            applicationId "org.xtimms.kitsune"
            minSdkVersion 21
            targetSdkVersion 29
            versionCode versionBuild
            versionName "${versionMajor}.${versionMinor}.${versionPatch} (${versionBuild})"

            resConfigs "en", "ru", "tr", "uk"
            buildConfigField "String", "SYNC_URL", "\"https://xtimms.pythonanywhere.com/api/v2\""
            buildConfigField "long", "TIMESTAMP", "${timestamp}L"
        }
    } else {
        throw new GradleException("Could not read version.properties!")
    }

    buildTypes {
        release {
            minifyEnabled false
            resValue "string", "app_name", "Kitsune"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [ appNameSuffix:""]
            applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "kitsune_v${defaultConfig.versionName}.apk"
                }
            }
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix '-DEBUG'
            resValue "string", "app_name", "Kitsune Debug"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    lintOptions {
        disable 'MissingTranslation'
        abortOnError false
    }

}

dependencies {
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.exifinterface:exifinterface:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation 'io.reactivex:rxjava:1.1.0'
    implementation 'com.squareup.okhttp3:okhttp:4.8.1'
    implementation 'com.squareup.duktape:duktape-android:1.3.0'
    implementation 'info.guardianproject.netcipher:netcipher:2.0.0-beta1'
    implementation 'info.guardianproject.netcipher:netcipher-okhttp3:2.0.0-alpha1'
    implementation 'org.jsoup:jsoup:1.12.1'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.bignerdranch.android:recyclerview-multiselect:0.2'
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.10.0'
    implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    implementation 'com.soundcloud.android:android-crop:1.0.1@aar'
    implementation 'com.lucasurbas:listitemview:1.1.1'
    implementation 'de.hdodenhof:circleimageview:3.0.2'
    implementation 'com.github.castorflex.smoothprogressbar:library:1.3.0'
    implementation 'me.zhanghai.android.fastscroll:library:1.1.1'
    implementation 'com.yandex.android:mobmetricalib:3.15.0'
    implementation 'org.koin:koin-android:2.2.0-alpha-1'
    implementation "androidx.core:core-ktx:1.3.1"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.9'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.9'
}
repositories {
    mavenCentral()
}
